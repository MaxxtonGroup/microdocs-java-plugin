apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'maven-publish'

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  api gradleApi()
  api("com.fasterxml.jackson.core:jackson-databind:2.10.1")
  api("com.mashape.unirest:unirest-java:1.4.9")
  api("com.maxxton:microdocs-core-java:3.0.0")
}

group = 'com.maxxton'
version = '3.0.0'


task fatJar(type: Jar, dependsOn: 'jar') {
  archiveBaseName = project.name
  configurations.api.setCanBeResolved(true)
  from { configurations.api.collect { it.isDirectory() ? it : zipTree(it) } }
  include('com/maxxton/**')
  include('com/fasterxml/**')
  include('com/mashape/**')
  include('META-INF/gradle-plugins/microdocs.properties')
  include('microdocs-crawler-doclet.jar')
  with jar
}

manifest {
  attributes 'Implementation-Title': 'microdocs-crawler-gradle', 'Implementation-Version': version
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier = 'sources'
}

artifacts {
  archives fatJar

  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'MicroDocs Gradle Crawler'
        description = 'MicroDocs crawler for gradle projects'
        url = 'https://github.com/MaxxtonGroup/microdocs'
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = 's.hermans'
            name = 'Steven Hermans'
          }
        }
        scm {
          url = 'scm:git@github.com:MaxxtonGroup/microdocs.git'
          connection = 'scm:git@github.com:MaxxtonGroup/microdocs.git'
          developerConnection = 'scm:git@github.com:MaxxtonGroup/microdocs.git'
        }
      }
    }
  }
  repositories {
    // switch local with remote repo for local development
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = sonatypeUsername
        password = sonatypePassword
      }
    }
//    mavenLocal()
  }
}
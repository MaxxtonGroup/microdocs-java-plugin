apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'maven-publish'

group 'com.maxxton'
version '3.0.0'

repositories {
  mavenCentral()
}

dependencies {
  api("com.fasterxml.jackson.core:jackson-databind:2.10.1")
  api("com.mashape.unirest:unirest-java:1.4.9")
  testImplementation("junit:junit:4.12")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier = "sources"
}

artifacts {
  archives jar

  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
  sign publishing.publications
}

java {
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'MicroDocs Core Java'
        description = 'MicroDocs core functionalities to share accross various projects'
        url = 'https://github.com/MaxxtonGroup/microdocs'
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = 's.hermans'
            name = 'Steven Hermans'
          }
        }
        scm {
          url = 'scm:git@github.com:MaxxtonGroup/microdocs.git'
          connection = 'scm:git@github.com:MaxxtonGroup/microdocs.git'
          developerConnection = 'scm:git@github.com:MaxxtonGroup/microdocs.git'
        }
      }
    }
  }
  repositories {
    // switch local with remote repo for local development
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = sonatypeUsername
        password = sonatypePassword
      }
    }
//    mavenLocal()
  }
}
